//
//   Copyright 2018  SenX S.A.S.
//
//   Licensed under the Apache License, Version 2.0 (the "License");
//   you may not use this file except in compliance with the License.
//   You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
//   Unless required by applicable law or agreed to in writing, software
//   distributed under the License is distributed on an "AS IS" BASIS,
//   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//   See the License for the specific language governing permissions and
//   limitations under the License.
//

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id 'io.codearte.nexus-staging' version '0.22.0'
}
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "signing"
apply plugin: "maven"

def getVersionName(String revision) {
    String newRevision = revision

    if (null == System.getProperty("nogit") && !revision.endsWith('SNAPSHOT')) {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--abbrev=0'
            standardOutput = stdout
        }
        newRevision = stdout.toString().trim()
    }
    return newRevision
}

def getShortVersionName(String revision) {
    if (null != revision) {
        if (revision.endsWith('SNAPSHOT')) {
            return revision
        }
    }

    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--abbrev=0'
        standardOutput = stdout
    }
    return stdout.toString().trim();
}

project.ext.group = 'io.warp10'
project.ext.uberjar = null != System.getProperty('uberjar')
project.ext.name = 'warp10-plugin-warpstudio'
project.ext.version = getVersionName('0.0.0')
version project.ext.version
group = project.ext.group
archivesBaseName = project.ext.name


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()
}


configurations {
    provided
}

dependencies {
    provided 'io.warp10:warpscript:+'
    compile 'io.warp10:warp10-warpstudio-server:2.0.2' // + project.ext.version
}

sourceSets {
    main {
        compileClasspath = compileClasspath + configurations.provided
    }
}

idea {
    module {
        inheritOutputDirs = true
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

if (project.hasProperty("ossrhUsername")) {
    nexusStaging {
        packageGroup = "io.warp10"
        username ossrhUsername
        password ossrhPassword
    }
}


shadowJar {
    manifest {
        attributes(
                'Main-Class': 'io.warp10.warpstudio.Main'
        )
        attributes('WarpFleet-Description': 'WarpStudioâ„¢ plugin')
        attributes('WarpFleet-Group': project.ext.group)
        attributes('WarpFleet-Artifact': project.ext.name)
        attributes('WarpFleet-Version': project.ext.version)
    }
    relocate 'org', 'io.warp10.warpstudio.org'
    relocate 'jetty', 'io.warp10.warpstudio.jetty'
    classifier = null
    from('.') {
        include 'README.md'
        include 'wf.json'
        include 'warp10.conf'
    }
}

artifacts {
    archives shadowJar, javadocJar, sourcesJar
}

if (project.hasProperty("signing.gnupg.keyName")) {
    signing {
        useGpgCmd()
        sign publishing.publications
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.ext.group
            artifactId project.ext.name
            version getShortVersionName()
            from components.java
            pom {
                name = project.ext.name
                description = 'WarpStudio, the WarpScript editor'
                url = 'https://warp10.io/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'senx'
                        name = 'SenX'
                        email = 'contact@senx.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:senx/warp10-plugin-warpstudio.git'
                    developerConnection = 'scm:git:git://github.com:senx/warp10-plugin-warpstudio.git'
                    url = 'https://github.com/senx/warp10-plugin-warpstudio.git'
                }
            }
        }
    }

    repositories {
        if (project.hasProperty("ossrhUsername")) {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
        if (project.hasProperty("nexusUsername")) {
            maven {
                name "nexus"
                url "$nexusHost/repository/maven-releases/"
                credentials {
                    username nexusUsername
                    password nexusPassword
                }
            }
        }
    }
}

uploadArchives {
    if (project.hasProperty("ossrhUsername")) {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                pom.project {
                    name project.ext.name
                    packaging 'jar'
                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    description 'WarpStudio, the WarpScript editor'
                    url 'https://warp10.io'

                    scm {
                        url 'https://github.com/senx/warp10-plugin-warpstudio.git'
                    }

                    developers {
                        developer {
                            id 'senx'
                            name 'SenX'
                            email 'contact@senx.io'
                        }
                    }
                }
            }
        }
    }
}


