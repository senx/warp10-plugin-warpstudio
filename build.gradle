//
//   Copyright 2018  SenX S.A.S.
//
//   Licensed under the Apache License, Version 2.0 (the "License");
//   you may not use this file except in compliance with the License.
//   You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
//   Unless required by applicable law or agreed to in writing, software
//   distributed under the License is distributed on an "AS IS" BASIS,
//   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//   See the License for the specific language governing permissions and
//   limitations under the License.
//
import java.text.SimpleDateFormat

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

archivesBaseName = 'warp10-plugin-warpstudio'
def warpStudioVersion = 'v0.0.73'
version = warpStudioVersion
group = 'io.warp10'

sourceCompatibility = 1.7
targetCompatibility = 1.7

//
// Repositories for dependency resolution
//
repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://repository.apache.org/content/groups/public'
    }
    maven {
        url "http://maven.twttr.com"
    }

    maven {
        url 'https://dl.bintray.com/senx/maven'
    }

    maven {
        url 'https://dl.bintray.com/senx/generic'
    }

    maven {
        url 'https://dl.bintray.com/hbs/maven'
    }

    maven {
        url "http://nexus.bedatadriven.com/content/groups/public/"
    }
}

configurations {
    provided
}

dependencies {
    provided group:'io.warp10', name: 'warpscript', version: '2.0.3'
   // compile group:'io.warp10', name: 'warp10-warpstudio-server', version: warpStudioVersion
    compile files('/home/xavier/workspace/warp-studio/server/build/libs/warp10-warpstudio-server.jar')
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

idea {
    module {
        inheritOutputDirs = true
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

shadowJar {
    manifest {
        attributes(
                'Main-Class': 'io.warp10.warpstudio.Main'
        )
    }
    relocate 'org', 'io.warp10.warpstudio.org'
    relocate 'jetty', 'io.warp10.warpstudio.jetty'
    relocate 'javax', 'io.warp10.warpstudio.javax'
    classifier = null
}

publishing {
    publications {
        WarpStudioPlugin(MavenPublication) {
            artifact shadowJar {
                extension = 'jar'
            }
            groupId 'io.warp10'
            artifactId archivesBaseName
            version warpStudioVersion
        }
    }
}

// DEPLOY ON BINTRAY
bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    dryRun = false
    publish = true

    publications = ['WarpStudioPlugin']

    pkg {
        repo = 'maven'
        name = archivesBaseName
        userOrg = 'senx'
        licenses = [ 'Apache 2.0' ]
        vcsUrl = 'https://gitlab.com/senx/warp10-plugin-warpstudio'
        version {
            name = warpStudioVersion
            released  = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZZ").format(new Date())
            vcsTag = warpStudioVersion
        }
    }
}

artifacts {
    archives shadowJar
}
